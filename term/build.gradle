/// ‘/term/build.gradle’ - module build file
/// 
/// See ‘/1-BUILD.txt’.

apply plugin: 'com.android.application'

// ============================================================
// Query for the certificate store password.
// ============================================================
// 
// From
//   https://www.timroes.de/2014/01/19
//     /using-password-prompts-with-gradle-build-files/
//
// - TODO Refactor into separate file.
//
// - TODO This will also query for the password when startet from the
//   IDE Android Studio's menu "Build" / "Generate signed APK", where
//   you already have specified the password in the IDE.

// Switch headless AWT off *before* including Groovy's ‘SwingBuilder’.
System.setProperty("java.awt.headless", "false");
import groovy.swing.SwingBuilder

gradle.taskGraph.whenReady { taskGraph ->
  if(taskGraph.hasTask(':term:assembleProdRelease')) {

    def password = ''
    new SwingBuilder().edt {
      dialog(
        modal:       true,          // Wait until you close the dialog.
        title:       'Enter password',
        alwaysOnTop: true, 
        resizable:   false,
        locationRelativeTo: null,   // Place dialog in center of the screen.
        pack:        true, 
        show:        true  
      ) {
        vbox {                      // Put everything below each other.
          label(text: "Please enter key passphrase:")
          input = passwordField()
          button(
            defaultButton: true, 
            text: 'OK', 
            actionPerformed: {
              // Convert array ‘input.password’ to a string.
              password = '' + input.password;
              dispose();            // Close dialog.
          })
    } } }

    if(password.size() <= 0) {
      throw new InvalidUserDataException(
        "You must enter a password to proceed.")
    }

    android.signingConfigs.release.storePassword = password
    android.signingConfigs.release.keyPassword   = password
  }
}

// ============================================================

android {
  //T+{ ------------------------------------------------------------
  // Add builds ‘dev’ and ‘prod’, part 1.
  signingConfigs {

    release {
      // Define ‘ANDROID_STORE_FILE’ in the global Gradle config file,
      // e.g.
      //   ‘c:\Users\«user»\.gradle\gradle.properties’.
         storeFile     file(ANDROID_STORE_FILE)

      // TODO Doesn't work any longer.
      // storePassword              // Set above.

         keyAlias      'tterm'

      // TODO Doesn't work any longer.
      // keyPassword                // Set above.
    }
  }
  //T+} ------------------------------------------------------------
}

android {
  compileSdkVersion 25

  buildToolsVersion "29.0.3"  // currently newest

  defaultConfig {
    applicationId    "de.t2h.tterm"
    minSdkVersion    19  // Android 4.4
    targetSdkVersion 25  // Android 7.1

    //T-{ ------------------------------------------------------------
    //T- ndk {
    //T-     moduleName "libjackpal-androidterm5"
    //T-     abiFilters 'armeabi', 'mips', 'x86'
    //T-     ldLibs "log"
    //T- }
    //T-} ------------------------------------------------------------
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 
        'proguard-rules.txt'

      //T+{ ------------------------------------------------------------
      // Add builds ‘dev’ and ‘prod’, part 2.
      signingConfig signingConfigs.release
      //T+} ------------------------------------------------------------
    }

    // Diable Crashlytics in ‘debug’.
    debug {
      ext.enableCrashlytics = false
    }
  }

  //T+{ ------------------------------------------------------------
  externalNativeBuild {
    ndkBuild {
      path 'src/main/jni/Android.mk'
    }
  }
  //T+} ------------------------------------------------------------

  dexOptions {
    preDexLibraries true
  }

  //T+{ ------------------------------------------------------------
  // Add builds ‘dev’ and ‘prod’, part 3.

  flavorDimensions "dev-or-prod"

  productFlavors {
    dev {
      dimension "dev-or-prod"
      minSdkVersion 22 // Android 5.1

      // This is necessary, and gives you separate settings for ‘dev’
      // and ‘prod’. Without it, you can't install ‘prod’ without
      // deinstalling ‘dev’ first, which deletes all TTerm Shortcuts
      // on your Home Screens.
      applicationIdSuffix ".dev"
      versionNameSuffix   '-dev'

      resValue "string", "application_terminal", "TTerm debug"
      resValue "string", "activity_shortcut_create", "TTerm debug shortcut"
      ndk {
        abiFilters "armeabi-v7a" // Only compile for Ten.
      }
      resConfigs "en", "xhdpi"   // Only compile for Ten.
    }
    prod {
      dimension "dev-or-prod"
      resValue "string", "application_terminal", "TTerm (beta)"
      resValue "string", "activity_shortcut_create", "TTerm shortcut"
    }
  }
  //T+} ------------------------------------------------------------
}

dependencies {
  implementation 'com.android.support:support-annotations:21.0.0'
}

